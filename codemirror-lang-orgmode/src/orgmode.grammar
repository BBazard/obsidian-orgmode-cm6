@precedence {
  tryHeading @left,
  tryZerothSection @left
}

@top Program {
  ZerothSection?
  Block*
}

ZerothSection {
  !tryZerothSection
  CommentLine*
  PropertyDrawer?
  (sectionLineExcludingPropertyDrawer (sectionLine | CommentLine)*)?
}

Block {
  Heading Section?
}

// https://orgmode.org/worg/org-syntax.html#Headings
Heading {
  !tryHeading stars TodoKeyword? Priority? Title Tags? endofline
}

Section {
  (
    // at least a CommentLine or Planning
    (CommentLine | Planning)+ PropertyDrawer?
    (sectionLineExcludingPropertyDrawerAndPlanning (sectionLine | CommentLine)*)?
  ) | (
    // OR at least a PropertyDrawer
    PropertyDrawer
    (sectionLineExcludingPropertyDrawerAndPlanning (sectionLine | CommentLine)*)?
  ) | (
    // OR at least a sectionLineExcludingPropertyDrawerAndPlanning
    sectionLineExcludingPropertyDrawerAndPlanning (sectionLine | CommentLine)*
  )
}

// https://orgmode.org/worg/org-syntax.html#Property_Drawers
@external tokens propertydrawer_tokenizer from "./external-tokens" { PropertyDrawer }

@tokens {
  Priority { $[ \t]* "[" "#" $[a-zA-Z0-9] "]" $[ \t]* }
  Tags { ":" ($[a-zA-Z0-9_@#%]+ ":")+ }
  stars { "*"+ $[ \t]+ }
  CommentLine { "#" ![\n]* ("\n" | @eof) }
}

@external tokens title_tokenizer from "./external-tokens" { Title }

@external tokens todokeyword_tokenizer from "./external-tokens" { TodoKeyword }

// https://orgmode.org/worg/org-syntax.html#Planning
@external tokens planning_line_tokenizer from "./external-tokens" { Planning }

// https://orgmode.org/worg/org-syntax.html#Sections
@external tokens sectionlineexcludingpropertydrawerandplanning_tokenizer from "./external-tokens" { sectionLineExcludingPropertyDrawerAndPlanning }
@external tokens sectionlineexcludingpropertydrawer_tokenizer from "./external-tokens" { sectionLineExcludingPropertyDrawer }
@external tokens sectionline_tokenizer from "./external-tokens" { sectionLine }

// need to be last to not override something else
@external tokens endofline_tokenizer from "./external-tokens" { endofline }