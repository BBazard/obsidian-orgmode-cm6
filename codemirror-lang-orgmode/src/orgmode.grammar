@top Program {
  ZerothSection?
  Block*
}

ZerothSection {
  notStartOfHeading
  CommentLine*
  PropertyDrawer?
  (
    notStartOfPropertyDrawer
    (
      notStartOfHeading
      (paragraph)
    )+
  )?
}

Block {
  Heading Section?
}

// https://orgmode.org/worg/org-syntax.html#Headings
Heading {
  stars TodoKeyword? Priority? Title Tags? endofline
}

Title {
  sectionSpace? (titleObject sectionSpace?)*
}

Section {
  notStartOfHeading
  (CommentLine | Planning)*
  PropertyDrawer?
  (
    notStartOfPlanning notStartOfPropertyDrawer
    (
      notStartOfHeading
      (paragraph) // or GreaterBlock, etc...
    )+
  )?
}

paragraph {
  CommentLine
  |
  notStartOfComment (
    sectionSpace? (object sectionSpace?)* sectionEnd
  )
}

titleObject {
  titleTextMarkup | titleWord
}

object {
  textMarkup | sectionword
}

titleTextMarkup {
  (isStartOfTitleTextBold TextBold) |
  (isStartOfTitleTextItalic TextItalic) |
  (isStartOfTitleTextUnderline TextUnderline) |
  (isStartOfTitleTextVerbatim TextVerbatim) |
  (isStartOfTitleTextCode TextCode) |
  (isStartOfTitleTextStrikeThrough TextStrikeThrough)
}

textMarkup {
  (isStartOfTextBold TextBold) |
  (isStartOfTextItalic TextItalic) |
  (isStartOfTextUnderline TextUnderline) |
  (isStartOfTextVerbatim TextVerbatim) |
  (isStartOfTextCode TextCode) |
  (isStartOfTextStrikeThrough TextStrikeThrough)
}

TextBold {
  "*"
  sectionwordBold (sectionSpace sectionwordBold)*
  "*"
}

TextItalic {
  "/"
  sectionwordItalic (sectionSpace sectionwordItalic)*
  "/"
}

TextUnderline {
  "_"
  sectionwordUnderline (sectionSpace sectionwordUnderline)*
  "_"
}

TextVerbatim {
  "="
  sectionwordVerbatim (sectionSpace sectionwordVerbatim)*
  "="
}

TextCode {
  "~"
  sectionwordCode (sectionSpace sectionwordCode)*
  "~"
}

TextStrikeThrough {
  "+"
  sectionwordStrikeThrough (sectionSpace sectionwordStrikeThrough)*
  "+"
}

@external tokens isStartOfTitleTextBold_lookaround from "./external-tokens" { isStartOfTitleTextBold }
@external tokens isStartOfTitleTextItalic_lookaround from "./external-tokens" { isStartOfTitleTextItalic }
@external tokens isStartOfTitleTextUnderline_lookaround from "./external-tokens" { isStartOfTitleTextUnderline }
@external tokens isStartOfTitleTextVerbatim_lookaround from "./external-tokens" { isStartOfTitleTextVerbatim }
@external tokens isStartOfTitleTextCode_lookaround from "./external-tokens" { isStartOfTitleTextCode }
@external tokens isStartOfTitleTextStrikeThrough_lookaround from "./external-tokens" { isStartOfTitleTextStrikeThrough }

@external tokens isStartOfTextBold_lookaround from "./external-tokens" { isStartOfTextBold }
@external tokens isStartOfTextItalic_lookaround from "./external-tokens" { isStartOfTextItalic }
@external tokens isStartOfTextUnderline_lookaround from "./external-tokens" { isStartOfTextUnderline }
@external tokens isStartOfTextVerbatim_lookaround from "./external-tokens" { isStartOfTextVerbatim }
@external tokens isStartOfTextCode_lookaround from "./external-tokens" { isStartOfTextCode }
@external tokens isStartOfTextStrikeThrough_lookaround from "./external-tokens" { isStartOfTextStrikeThrough }

// https://orgmode.org/worg/org-syntax.html#Planning
Planning { planningStart endofline }

// https://orgmode.org/worg/org-syntax.html#Property_Drawers
@external tokens propertydrawer_tokenizer from "./external-tokens" { PropertyDrawer }

@external tokens todokeyword_tokenizer from "./external-tokens" { TodoKeyword }


@external tokens notStartOfPlanning_lookaround from "./external-tokens" { notStartOfPlanning }
@external tokens notStartOfPropertyDrawer_lookaround from "./external-tokens" { notStartOfPropertyDrawer }

@external tokens notStartOfHeading_lookaround from "./external-tokens" { notStartOfHeading }
@external tokens notStartOfComment_lookaround from "./external-tokens" { notStartOfComment }


@tokens {
  Priority { $[ \t]* "[" "#" $[a-zA-Z0-9] "]" $[ \t]* }
  stars { "*"+ $[ \t]+ }
  CommentLine { "#" ![\n]* ("\n" | @eof) }
  planningStart { ("DEADLINE:"|"SCHEDULED:"|"CLOSED:") }
}

@external tokens sectionEnd_tokenizer from "./external-tokens" { sectionEnd }
@external tokens sectionSpace_tokenizer from "./external-tokens" { sectionSpace }
@external tokens titleWord_tokenizer from "./external-tokens" { titleWord }
@external tokens tags_tokenizer from "./external-tokens" { Tags }

// need to be last to not override something else
@external tokens endofline_tokenizer from "./external-tokens" { endofline }

// https://orgmode.org/worg/org-syntax.html#Sections
@external tokens sectionWord_tokenizer from "./external-tokens" { sectionword }

@external tokens sectionWordBold_tokenizer from "./external-tokens" { sectionwordBold }
@external tokens sectionWordItalic_tokenizer from "./external-tokens" { sectionwordItalic }
@external tokens sectionWordUnderline_tokenizer from "./external-tokens" { sectionwordUnderline }
@external tokens sectionWordVerbatim_tokenizer from "./external-tokens" { sectionwordVerbatim }
@external tokens sectionWordCode_tokenizer from "./external-tokens" { sectionwordCode }
@external tokens sectionWordStrikeThrough_tokenizer from "./external-tokens" { sectionwordStrikeThrough }

