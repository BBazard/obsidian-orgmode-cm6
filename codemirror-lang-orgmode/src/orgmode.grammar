@top Program {
  ZerothSection?
  Block*
}

ZerothSection {
  notStartOfHeading
  CommentLine*
  PropertyDrawer?
  (notStartOfPropertyDrawer sectionLine+)?
}

Block {
  Heading Section?
}

// https://orgmode.org/worg/org-syntax.html#Headings
Heading {
  stars TodoKeyword? Priority? Title Tags? endofline
}

Section {
  notStartOfHeading
  (CommentLine | Planning)*
  PropertyDrawer?
  (notStartOfPlanning notStartOfPropertyDrawer sectionLine+)?
}

sectionLine {
  notStartOfHeading
  (
    CommentLine
    |
    notStartOfComment (
      sectionSpace? (sectionword sectionSpace?)* sectionEnd
    )
  )
}

// https://orgmode.org/worg/org-syntax.html#Planning
Planning { planningStart endofline }

// https://orgmode.org/worg/org-syntax.html#Property_Drawers
@external tokens propertydrawer_tokenizer from "./external-tokens" { PropertyDrawer }

@external tokens title_tokenizer from "./external-tokens" { Title }

@external tokens todokeyword_tokenizer from "./external-tokens" { TodoKeyword }


@external tokens notStartOfPlanning_lookaround from "./external-tokens" { notStartOfPlanning }
@external tokens notStartOfPropertyDrawer_lookaround from "./external-tokens" { notStartOfPropertyDrawer }

@external tokens notStartOfHeading_lookaround from "./external-tokens" { notStartOfHeading }
@external tokens notStartOfComment_lookaround from "./external-tokens" { notStartOfComment }

// need to be between after Section and before endofline
@tokens {
  Priority { $[ \t]* "[" "#" $[a-zA-Z0-9] "]" $[ \t]* }
  Tags { ":" ($[a-zA-Z0-9_@#%]+ ":")+ }
  stars { "*"+ $[ \t]+ }
  CommentLine { "#" ![\n]* ("\n" | @eof) }
  planningStart { ("DEADLINE:"|"SCHEDULED:"|"CLOSED:") }
}

// need to be last to not override something else
@external tokens endofline_tokenizer from "./external-tokens" { endofline }

// https://orgmode.org/worg/org-syntax.html#Sections
@external tokens sectionWord_tokenizer from "./external-tokens" { sectionword }
@external tokens sectionSpace_tokenizer from "./external-tokens" { sectionSpace }
@external tokens sectionEnd_tokenizer from "./external-tokens" { sectionEnd }
